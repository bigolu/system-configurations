# TODO: Consider upstreaming

{
  pkgs,
  config,
  lib,
  ...
}:
let
  inherit (lib)
    types
    mkOption
    escapeShellArg
    makeBinPath
    optionalAttrs
    ;

  # Adds or removes a line from a system file
  makeSnippet =
    {
      isEnabled,
      file,
      marker,
      line,
    }:
    let
      setPath =
        snippet:
        let
          dependencies = with pkgs; [
            coreutils
            gnused
            gnugrep
          ];
          pathEntriesForDependencies = makeBinPath dependencies;
        in
        ''
          OLD_PATH="$PATH"
          PATH="${pathEntriesForDependencies}:$PATH"
          ${snippet}
          PATH="$OLD_PATH"
        '';

      snippet =
        if isEnabled then
          ''
            touch ${file}
            if ! grep '${marker}' ${file} > /dev/null; then
              # shellcheck disable=2016
              printf \
                '\n%s\n' \
                ${escapeShellArg line}' # generated by nix-darwin: ${marker}' \
                | sudo tee -a ${file}
            fi
          ''
        else
          ''
            touch ${file}
            if grep '${marker}' ${file} > /dev/null; then
              sed -i '/${marker}/d' ${file}
            fi
          '';
    in
    setPath snippet;

  nixDarwinLoginShellSnippet = makeSnippet {
    isEnabled = config.configureLoginShellForNixDarwin;
    file = "/etc/zprofile";
    marker = "configureLoginShellForNixDarwin";
    line = ''export PATH="/run/current-system/sw/bin:$PATH"'';
  };
in
{
  options.configureLoginShellForNixDarwin = mkOption {
    type = types.bool;
    default = false;
  };

  config = {
    system.activationScripts.postActivation.text = ''
      printf '\e[36m┃ [bigolu] Configuring login shell for nix-darwin ❯\e(B\e[m\n' >&2
      ${nixDarwinLoginShellSnippet}
    '';

    # Reapply the changes if they get removed, which can happen during a macOS
    # update[1]. Adapted from nix-darwin[2].
    #
    # [1]: https://determinate.systems/posts/nix-survival-mode-on-macos/
    # [2]: https://github.com/LnL7/nix-darwin/blob/230a197063de9287128e2c68a7a4b0cd7d0b50a7/modules/services/activate-system/default.nix#L20
    launchd.daemons = optionalAttrs config.configureLoginShellForNixDarwin {
      restore-profile-changes = {
        serviceConfig.RunAtLoad = true;
        serviceConfig.KeepAlive.SuccessfulExit = false;

        script = ''
          set -o errexit
          set -o pipefail
          ${nixDarwinLoginShellSnippet}
        '';
      };
    };
  };
}
